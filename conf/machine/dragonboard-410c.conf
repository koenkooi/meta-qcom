#@TYPE: Machine
#@NAME: 96boards DragonBoard 410c machine
#@DESCRIPTION: 96boards

require conf/machine/include/arm/arch-armv8.inc


XSERVER_OPENGL ?= "xf86-video-freedreno \
                   xserver-xorg-module-exa \
                   xserver-xorg-extension-glx \
                   xserver-xorg-extension-dri \
                  "

XSERVER ?= "xserver-xorg \
            ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', '${XSERVER_OPENGL}', 'xf86-video-fbdev', d)} \
            xf86-input-evdev \
            xf86-input-mouse \
            xf86-input-keyboard"

MACHINE_FEATURES = "usbhost usbgadget alsa screen wifi bluetooth efi"

PREFERRED_PROVIDER_virtual/egl ?= "mesa"
PREFERRED_PROVIDER_virtual/libgl ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles1 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles2 ?= "mesa"
PREFERRED_PROVIDER_virtual/xserver ?= "xserver-xorg"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-linaro-qcomlt"

KERNEL_IMAGETYPE = "Image"
KERNEL_DEVICETREE = "${S}/arch/arm64/boot/dts/dragonboard.dts"

SERIAL_CONSOLE = "115200 ttyMSM0"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
                                        ${@bb.utils.contains('DISTRO_FEATURES', 'wifi', 'kernel-module-wcn36xx kernel-module-wcn36xx-platform', '', d)} \
                                        ${@bb.utils.contains('DISTRO_FEATURES', 'opengl', 'mesa-driver-msm', '', d)} \
                                        ${@'firmware-qcom-dragonboard410c' if d.getVar('ACCEPT_QCOM_EULA', True) == '1' else ''}"

CMDLINE = "console=ttyMSM0,115200n8 root=/dev/mmcblk0p10 rootwait"

# Fastboot expects an ext4 image, which needs to be 4096 aligned
IMAGE_FSTYPES_append = " ext4.gz"
IMAGE_ROOTFS_ALIGNMENT = "4096"

